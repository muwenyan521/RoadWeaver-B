plugins {
	id 'com.gradleup.shadow' version '8.3.6'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    // 访问转换器配置
    accessWidenerPath = project(':common').loom.accessWidenerPath
    
    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        
        mixinConfig "roadweaver.mixins.json"
    }

    // 数据生成运行配置
    runs {
        data {
            data()
            programArgs "--all", "--mod", "roadweaver"
            programArgs "--output", file("src/generated/resources").absolutePath
            programArgs "--existing", file("src/main/resources").absolutePath
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        url "https://maven.shedaniel.me/"
        name "Shedaniel Maven"
    }
    maven {
        url "https://maven.minecraftforge.net/"
        name "Forge"
    }
    maven {
        url "https://maven.architectury.dev/"
        name "Architectury"
    }
}

dependencies {
    // Common 模块
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Forge 平台
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    // Architectury API - 内嵌到模组中
    modImplementation("dev.architectury:architectury-forge:9.2.14") {
        exclude group: "net.minecraftforge"
    }
    include("dev.architectury:architectury-forge:9.2.14")

    // Cloth Config API for Forge (可选)
    modCompileOnly "me.shedaniel.cloth:cloth-config-forge:11.1.106"
    modRuntimeOnly "me.shedaniel.cloth:cloth-config-forge:11.1.106"
}

tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
        minecraft_version: project.minecraft_version,
        minecraft_version_range: '[1.20.1,1.21)',
        forge_version: project.forge_version,
        forge_version_range: '[47,)',
        loader_version_range: '[47,)',
        mod_id: 'roadweaver',
        mod_name: 'RoadWeaver',
        mod_version: project.version,
        mod_authors: 'Countered',
        mod_description: 'Automatically generates roads between structures'
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties
    }
}

shadowJar {
    exclude "architectury.common.json"
    exclude "fabric.mod.json"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
	inputFile.set shadowJar.archiveFile
	dependsOn shadowJar
}

jar {
	archiveClassifier.set("dev")
	from("${rootProject.projectDir}/LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
	
	manifest {
		attributes([
			'Specification-Title'     : 'RoadWeaver',
			'Specification-Vendor'    : 'Countered',
			'Specification-Version'   : '1',
			'Implementation-Title'    : project.name,
			'Implementation-Version'  : project.version,
			'Implementation-Vendor'   : 'Countered',
			'MixinConfigs'            : 'roadweaver.mixins.json'
		])
	}
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

sourceSets {
    main {
        java {
            exclude 'net/countered/settlementroads/features/config/RoadFeatureConfig.java'
            exclude 'net/countered/settlementroads/features/RoadFeature.java'
            exclude 'net/countered/settlementroads/events/ModEventHandler.java'
            exclude 'net/countered/settlementroads/features/config/RoadFeatureRegistry.java'
        }
        resources {
            srcDir 'src/generated/resources'
            exclude 'data/roadweaver/worldgen/configured_feature/road_feature.json'
            exclude 'data/roadweaver/worldgen/placed_feature/road_feature_placed.json'
        }
    }
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = "${rootProject.name}-forge"
			from components.java
		}
	}
	repositories {
		// 添加发布仓库
	}
}
